```julia
function optimize_ies_ele!(machines::Tuple, isOpt::Vector, fin::Financial, ::Val{1})
    pv, wt, ec, hc, _ = machines
    isOpt = isOpt[1:4]
    obj = function (x)
        # 顺序为光伏、风机、电解槽、储氢，顺序不能错！！！
        machines = matchOptVars((pv, wt, ec, hc), isOpt, x)
        wt_power, pv_power, load_power, hc_power = map(outputEnergy, machines)
        powers = (wt_power, pv_power, load_power, hc_power)
        # 供给-需求=ΔE
        ΔE = wt_power + pv_power - load_power - hc_power
        # 余电上网，网汇购电
        ΔE_to_grid, ΔE_from_grid = pn_split(ΔE)
        # 返回 "制氢价格（元/kg）"，该数值即为待优化的目标值
        dictData = economicAnalysisData(machines, fin, powers,
            (sum(ΔE_to_grid), sum(ΔE_from_grid), 0), Val(1))
        objective = -abs(1 / dictData["静态总投资回收年限（年）"])
        return objective
    end
    # 调用优化求解器，可查BlackBoxOptim.jl文档
    res = bboptimize(obj; SearchRange=(1e1, 1e7),
        NumDimensions=sum(isOpt), TraceMode=:verbose)
    # 输出优化结果
    candidate, fitness = best_candidate(res), best_fitness(res)
    println("优化变量结果： $candidate", "目标值： $fitness")
    machines = matchOptVars((pv, wt, ec, hc), isOpt, candidate)
    # 返回最优解的仿真结果
    return simulate_ies_ele!((machines..., 0), fin, Val(1))
end
```

```julia
function simulate_ies_ele!(machines::Tuple, fin::Financial, ::Val{1})
    pv, wt, ec, hc, _ = machines
    pv_power, wt_power, load_power = map(outputEnergy, (pv, wt, ec))
    # println(load_power)
    hc.load = outputH2Mass(load_power, ec, 1.0)
    hc_power = outputEnergy(hc)
    powers = (pv_power, wt_power, load_power, hc_power)
    # 供给-需求=ΔE
    ΔE = wt_power + pv_power - load_power - hc_power
    # 余电上网，网汇购电
    ΔE_to_grid, ΔE_from_grid = pn_split(ΔE)
    machines = (pv, wt, ec, hc)
    fd = figureDictData(wt_power, pv_power, load_power, hc_power,
        ΔE_to_grid, ΔE_from_grid, Val(1))
    ecd = economicAnalysisData(machines, fin, powers,
        (sum(ΔE_to_grid), sum(ΔE_from_grid), 0), Val(1))
    return fd, ecd
end
```

```julia
data_weather = CSV.File(joinpath(@__DIR__, "./CNPC/data/weather_lenghu_2018.csv"); select=["glob_hor_rad", "DBT", "wind_speed"])
const S1_DATA_GI = data_weather.glob_hor_rad
const S1_DATA_TA = data_weather.DBT
const S1_DATA_WS = data_weather.wind_speed
# const S1_DATA_LOAD = 5285.41 * ones(Float64, 8760)

data_weather = CSV.File(joinpath(@__DIR__, "./CNPC/data/weather_lenghu_2018.csv"); select=["dir_nor_rad", "DBT", "wind_speed"])

const S2_DATA_GI = data_weather.dir_nor_rad
const S2_DATA_TA = data_weather.DBT
const S2_DATA_WS = data_weather.wind_speed

# 跨域解决方案
const CORS_HEADERS = [
  "Access-Control-Allow-Origin" => "*",
  "Access-Control-Allow-Headers" => "*",
  "Access-Control-Allow-Methods" => "POST, GET, OPTIONS"
]

function CorsMiddleware(handler)
  return function (req::HTTP.Request)
    # println("CORS middleware")
    # determine if this is a pre-flight request from the browser
    if HTTP.method(req) ∈ ["POST", "GET", "OPTIONS"]
      return HTTP.Response(200, CORS_HEADERS, HTTP.body(handler(req)))
    else
      return handler(req) # passes the request to the AuthMiddleware
    end
  end
end

@post "/simulation" function (req)
  # 将HTTP请求的正文（request body）转换为 Julia 中的字典（Dict）数据结构
  paras = json(req)
  # 调用后端模型获得数据
  #table = simulate!(paras["inputdata"], Val(paras["mode"]))
  #println(paras)
  figure, table = simulate!(paras["inputdata"], Val(paras["mode"]))
  #println(figure)
  # 返回数据，匹配前端request要求的格式
  return Dict(
    "code" => 200,
    "message" => "success",
    "data" => Dict(
      # "table" => getTableData(table),
      "table" => OrderedDict(k => round(v, digits=2) for (k, v) in table),
      "figure" => Dict(
        "xyAxis" => figure,
      )
    ))
end
```

```julia
"""
风力发电机

组件参数:
- `input_v`: 环境风速输入
- `capacity`: 总装机容量， kW
- `unit_capacity`: 单机容量， kW
- `machine_number`: 机组数量
- `Δt`: 采样时间， h
- `η_t`: 风轮传动效率
- `η_g`: 发电机效率
- `h1`: 风速切入速度， m/s
- `h2`: 风速切出速度， m/s
- `α`: 风速指数
- `η_inverter`: 综合效率，如考虑逆变器、电机效率等
- `life_year`: 使用年限，年
- `cost_initial`: 初始成本，元/kW
- `cost_OM`: 年运维成本，元/kW
- `cost_replace`: 更换成本，元/kW
- `k `: 风速-功率曲线
- `staff_number`: 人员数

"""
Base.@kwdef mutable struct WindTurbine <: RenewableEnergyMachine
    input_v::Vector = Float64[]
    capacity::Float64 = 4e6
    unit_capacity::Float64 = 1.0
    machine_number::Int64 = 1
    Δt::Float64 = 1.0
    η_t::Float64 = 0.96
    η_g::Float64 = 0.93
    h1::Float64 = 10.0
    h2::Float64 = 135.0
    α::Float64 = 1.0 / 7.0
    η_inverter::Float64 = 1.0
    life_year::Float64 = 20.0
    cost_initial::Float64 = 4800.0
    cost_OM::Float64 = 720.0
    cost_replace::Float64 = 4800.0
    k::Function = k
    staff_number::Int64 = 50
end

k(v2) = ifelse(v2 < 3.0, 0.0,
    ifelse(3.0 <= v2 < 9.5, (-30.639 * v2^3 + 623.5 * v2^2 - 3130.4 * v2 + 4928) / 5000,
        ifelse(9.5 <= v2 < 19.5, 1.0,
            ifelse(19.5 <= v2 <= 25.0, (-203.97 * v2 + 9050.9) / 5000, 0.0))))

"""

光伏组件

组件参数:
- `input_GI`: 光照强度输入， Wh/m2
- `input_Ta`: 环境温度输入， ℃
- `input_v`: 风速输入， m/s
- `capacity`: 总装机容量， kW
- `unit_capacity`: 单机容量， kW
- `machine_number`: 机组数量
- `Δt`: 采样时间， h
- `A`: 光伏板面积， m2
- `f_PV`: 光伏板填充因子
- `η_PV_ref`: 光伏板额定转换效率
- `λ`: 光伏板温度系数
- `Tc_ref`: 光伏板额定温度， ℃
- `tau_alpha`: 光伏板吸收率
- `η_inverter`: 综合效率，如考虑逆变器、电机效率等
- `life_year`: 使用年限，年
- `cost_initial`: 初始成本，元/kW
- `cost_OM`: 年运维成本，元/kW
- `cost_replace`: 更换成本，元/kW
- `staff_number`: 人员数

"""
Base.@kwdef mutable struct PhotovoltaicCell <: RenewableEnergyMachine
    input_GI::Vector = Float64[]
    input_Ta::Vector = Float64[]
    input_v::Vector = Float64[]
    capacity::Float64 = 1e7
    unit_capacity::Float64 = 1.0
    machine_number::Int64 = 1
    Δt::Float64 = 1.0
    A::Float64 = 3.1
    f_PV::Float64 = 1 
    η_PV_ref::Float64 = 20.9 / 100
    λ::Float64 = -0.34 / 100
    Tc_ref::Float64 = 25.0
    tau_alpha::Float64 = 0.9
    η_inverter::Float64 = 0.98
    life_year::Float64 = 20.0
    cost_initial::Float64 = 3800.0
    cost_OM::Float64 = 190.0
    cost_replace::Float64 = 3800.0
    staff_number::Int64 = 50
end
```

